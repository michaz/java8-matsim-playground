configurations {
    wurst
}

dependencies {
    wurst project(':matsim-playground')
}

repositories {
    maven { url "http://download.java.net/maven/2" }
    maven { url "http://download.osgeo.org/webdav/geotools" }
    mavenCentral()
    maven { url "http://dl.bintray.com/matsim/matsim" }
    maven { url "http://oss.jfrog.org/artifactory/libs-snapshot" }
}

project (':hmm-lib') {
    apply plugin: 'java'
    repositories {
        mavenCentral()
    }
    dependencies {
        testCompile group: 'junit', name: 'junit', version:'4.8.2'
    }
}

project ('matsim-playground') {
    apply plugin: 'java'
    dependencies {
        compile project(':hmm-lib')
    }
}

def sample = 0.1
def Rscript = '/usr/local/bin/Rscript'

def truths = ['uncongested3', 'congested3']

task illustrative(type: JavaExec) {
    outputs.dir file('output/illustrative/8-count')
    outputs.dir file('output/illustrative/8-count-orig')
    outputs.dir file('output/illustrative/18-count')
    outputs.dir file('output/illustrative/18-count-orig')
    classpath = configurations.wurst
    main = 'stratum.Main'
}

task illustrative_plot(type: Exec, dependsOn: illustrative) {
    outputs.file file('output/illustrative/ill-count-joined.pdf')
    commandLine Rscript, 'r-src/ill-count-joined.R'
}

task uncongested3(type: JavaExec) {
    def input = file('input/berlin/2kW.15.xml')
    def output = file('output/berlin/uncongested3/output-berlin')
    classpath = configurations.wurst
    inputs.file input
    outputs.dir output
    main = 'berlin.BerlinRunUncongested3'
    args sample, input.getPath(), output.getPath()
}

task congested3(type: JavaExec) {
    def input = file('input/berlin/2kW.15.xml')
    def output = file('output/berlin/congested3/output-berlin')
    classpath = configurations.wurst
    inputs.file input
    outputs.dir output
    main = 'berlin.BerlinRunCongested3'
    args sample, input.getPath(), output.getPath()
}

def usages = ['random-heavy-users', 'worker-heavy-users', 'only-heavy-users']

truths.each { truth ->
    task (truth + "_random-heavy-users", type: JavaExec, dependsOn: truth) {
        def input = file("output/berlin/$truth/output-berlin")
        def output = file("output/berlin/$truth/alternatives/random-heavy-users/sightings")
        classpath = configurations.wurst
        inputs.dir input
        outputs.dir output
        main = 'populationsize.RunRandomHeavyUsers'
        args sample, input.getPath(), output.getPath()
    }
}

truths.each { truth ->
    task (truth + "_worker-heavy-users", type: JavaExec, dependsOn: truth) {
        def input = file("output/berlin/$truth/output-berlin")
        def output = file("output/berlin/$truth/alternatives/worker-heavy-users/sightings")
        classpath = configurations.wurst
        inputs.dir input
        outputs.dir output
        main = 'populationsize.RunWorkerNonWorker'
        args input.getPath(), output.getPath()
    }
}

truths.each { truth ->
    task (truth + "_only-heavy-users", type: JavaExec, dependsOn: truth) {
        def input = file("output/berlin/$truth/output-berlin")
        def output = file("output/berlin/$truth/alternatives/only-heavy-users/sightings")
        classpath = configurations.wurst
        inputs.dir input
        outputs.dir output
        main = 'populationsize.RunOnlyHeavyUsers'
        args input.getPath(), output.getPath()
    }
}

task ('simulate_uncongested3_only-heavy-users_segmentation-only', type: JavaExec, dependsOn: 'uncongested3_only-heavy-users') {
    def baseRun = file('output/berlin/uncongested3/output-berlin')
    def sightingsDir = file('output/berlin/uncongested3/alternatives/only-heavy-users/sightings')
    def output = file("output/berlin/uncongested3/alternatives/only-heavy-users/alternatives/segmentation-only/run")
    classpath = configurations.wurst
    main = 'segmenttraces.RunTrajectorySegmentationOnly'
    args baseRun.getPath(), sightingsDir.getPath(), output.getPath()
    inputs.dir baseRun
    inputs.dir sightingsDir
    outputs.dir output
}

def alternatives = ['full-procedure', 'full-procedure-with-histogram', 'clone', 'clone-with-histogram']
truths.each { truth ->
    usages.each { usage ->
        alternatives.each { alternative ->
            def baseRun = file("output/berlin/$truth/output-berlin")
            def run = file("output/berlin/$truth/alternatives/$usage/alternatives/$alternative/run")
            Task simulate = task("simulate_" + truth + "_" + usage + "_" + alternative, type: JavaExec, dependsOn: truth + "_" + usage) {
                def sightings = file("output/berlin/$truth/alternatives/$usage/sightings")
                classpath = configurations.wurst
                inputs.dir baseRun
                inputs.dir sightings
                outputs.dir run
                main = 'populationsize.RunSimulation'
                args alternative, baseRun.getPath(), sightings.getPath(), run.getPath()
            }
            task ("histogram_" + truth + "_" + usage + "_" + alternative, type: Exec, dependsOn: simulate) {
                def txt = file("output/berlin/$truth/travel-distance-histo_" + usage + "_" + alternative+ ".txt")
                def pdf = file("output/berlin/$truth/travel-distance-histo_" + usage + "_" + alternative+ ".pdf")
                def png = file("output/berlin/$truth/travel-distance-histo_" + usage + "_" + alternative+ ".png")
                inputs.dir baseRun
                inputs.dir run
                outputs.file txt
                outputs.file pdf
                outputs.file png
                commandLine Rscript, 'r-src/histogram.R', baseRun.getPath(), run.getPath(), txt.getPath(), pdf.getPath(), png.getPath()
            }
            task ("en-route_" + truth + "_" + usage + "_" + alternative, type: Exec, dependsOn: simulate) {
                def pdf = file("output/berlin/$truth/alternatives/$usage/alternatives/$alternative/en-route.pdf")
                def png = file("output/berlin/$truth/alternatives/$usage/alternatives/$alternative/en-route.png")
                inputs.dir baseRun
                inputs.dir run
                outputs.file pdf
                outputs.file png
                commandLine Rscript, 'r-src/en-route.R', baseRun.getPath(), run.getPath(), pdf.getPath(), png.getPath()
            }
        }
    }
}

task guiceDot (type: JavaExec) {
    def input = file('input/graphconfig.xml')
    def output = file('output/guice')
    classpath = configurations.wurst
    inputs.file input
    outputs.dir output
    main = 'guice.GraphReduced'
    args input.getPath(), 'guice.MyModule', output.getPath()
}

task guice (type: Exec, dependsOn: guiceDot) {
    commandLine "dot", "-Tpdf", "-ooutput/guice/guice.pdf", "output/guice/guice.dot"
}

task legHistogramApp(type: JavaExec, dependsOn: uncongested3) {
    def input = file('output/berlin/uncongested3/output-berlin')
    classpath = configurations.wurst
    main = 'fx.LegHistogramApp'
    args '--runDir='+input.getPath()
    inputs.dir input
}

task trajectorySimilarityApp(type: JavaExec, dependsOn: 'uncongested3_worker-heavy-users') {
    def baseRun = file('output/berlin/uncongested3/output-berlin')
    def sightingsDir = file('output/berlin/uncongested3/alternatives/worker-heavy-users/sightings')
    classpath = configurations.wurst
    main = 'fx.TrajectorySimilarityApp'
    args '--baseRunDir='+baseRun.getPath(), '--sightingsDir='+sightingsDir.getPath()
    inputs.dir baseRun
    inputs.dir sightingsDir
}

task trajectorySimilarityFigures(type: JavaExec, dependsOn: 'uncongested3_worker-heavy-users') {
    def baseRun = file('output/berlin/uncongested3/output-berlin')
    def sightingsDir = file('output/berlin/uncongested3/alternatives/worker-heavy-users/sightings')
    def output = file('output/berlin/uncongested3/alternatives/worker-heavy-users/trajectory-similarity-figures')
    classpath = configurations.wurst
    main = 'fx.RunTrajectorySimilarityFigures'
    args '--baseRunDir='+baseRun.getPath(), '--sightingsDir='+sightingsDir.getPath(), '--output='+output.getPath(), '--sparse=11161851', '--dense=16203334'
    inputs.dir baseRun
    inputs.dir sightingsDir
    outputs.dir output
}

task activityStructure(type: JavaExec, dependsOn: 'simulate_uncongested3_only-heavy-users_segmentation-only') {
    def baseRun = file('output/berlin/uncongested3/output-berlin')
    def sightingsDir = file('output/berlin/uncongested3/alternatives/only-heavy-users/sightings')
    def run = file('output/berlin/uncongested3/alternatives/only-heavy-users/alternatives/segmentation-only/run')
    def png = file('output/berlin/uncongested3/alternatives/only-heavy-users/alternatives/segmentation-only/activity-structure.png')
    classpath = configurations.wurst
    main = 'segmenttraces.RunActivityStructure'
    args baseRun.getPath(), sightingsDir.getPath(), run.getPath(), png.getPath()
    inputs.dir baseRun
    inputs.dir sightingsDir
    inputs.dir run
    outputs.file png
}

task activityStructureDistance(type: JavaExec, dependsOn: 'simulate_uncongested3_only-heavy-users_segmentation-only') {
    def baseRun = file('output/berlin/uncongested3/output-berlin')
    def sightingsDir = file('output/berlin/uncongested3/alternatives/only-heavy-users/sightings')
    def run = file('output/berlin/uncongested3/alternatives/only-heavy-users/alternatives/segmentation-only/run')
    def png = file('output/berlin/uncongested3/alternatives/only-heavy-users/alternatives/segmentation-only/activity-structure-distance.png')
    classpath = configurations.wurst
    main = 'segmenttraces.RunActivityStructureDistance'
    args '--baseRunDir='+baseRun.getPath(), '--runDir='+run.getPath(), '--sightingsDir='+sightingsDir.getPath(), '--output='+png.getPath(), '--headless=true'
    inputs.dir baseRun
    inputs.dir run
    outputs.file png
}

task activityStructureDistanceApp(type: JavaExec, dependsOn: 'simulate_uncongested3_only-heavy-users_segmentation-only') {
    def baseRun = file('output/berlin/uncongested3/output-berlin')
    def sightingsDir = file('output/berlin/uncongested3/alternatives/only-heavy-users/sightings')
    def run = file('output/berlin/uncongested3/alternatives/only-heavy-users/alternatives/segmentation-only/run')
    def png = file('output/berlin/uncongested3/alternatives/only-heavy-users/alternatives/segmentation-only/activity-structure-distance.png')
    classpath = configurations.wurst
    main = 'segmenttraces.RunActivityStructureDistance'
    args '--baseRunDir='+baseRun.getPath(), '--runDir='+run.getPath(), '--sightingsDir='+sightingsDir.getPath(), '--output='+png.getPath()
    inputs.dir baseRun
    inputs.dir run
    outputs.file png
}


